plugins {
    id 'com.android.application'
}

tasks.configureEach { task ->
    if (task.name == 'assembleRelease') {
//        task.dependsOn('connectedAndroidTest')
        task.dependsOn('testDebugUnitTest')
    }

    if (task.name == 'assembleDebug')
        task.dependsOn('testDebugUnitTest')

}

android {
    ndkVersion '27.1.12297006'

    defaultConfig {
        applicationId "com.paddlesandbugs.dahdidahdit"
        compileSdk 34
        minSdkVersion 24
        targetSdkVersion 34
        versionCode appVersionCode
        versionName appVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        configureEach {
            resValue "string", "app_version_name", appVersionName
        }

        release {
            minifyEnabled true // Try @Keep on classes and methods that get pruned but shouldn't
        }

        debug {
            pseudoLocalesEnabled true
        }
    }

    externalNativeBuild {
        cmake {
            version '3.30.5'
            path 'src/main/cpp/CMakeLists.txt'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        viewBinding true
    }

    namespace 'com.paddlesandbugs.dahdidahdit'
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.preference:preference:1.2.1'

    def room_version = "2.6.1"

    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.6.0'
}


tasks.register('tidy', Delete) {
    delete project.buildDir
}


tasks.register('apk') {
	group = "Build"
	description = "Generates a signed APK ready for installing"

	def Properties properties = new Properties()
	properties.load(project.rootProject.file("local.properties").newDataInputStream())
	def String keystoreDir = properties.getProperty("keystore.dir", "")

	def outputDir = "${buildDir}/outputs/apk/release/";
	def apkName = "dahdidahdit-${project.android.defaultConfig.versionCode}.apk";
	println("Building APK")
	println("Project name: ${project.name}")
	println("Build dir: ${buildDir}")
	println("SDK dir: ${project.android.sdkDirectory}")
	println("Version: ${project.android.defaultConfig.versionCode}")
	println("Keystore dir: ${keystoreDir}")

	exec {
		workingDir "${outputDir}"
		executable "${project.android.sdkDirectory}/build-tools/33.0.1/zipalign"
		args '-v', '-p', '4', '-f', "app-release-unsigned.apk", "dahdidahdit-unsigned-aligned.apk"
	}
	exec {
		workingDir "${outputDir}"
		executable "${project.android.sdkDirectory}/build-tools/33.0.1/apksigner"
		args 'sign', '--ks',  "${keystoreDir}/android-keystore-dahdidahdit-production.jks", '--out', "${apkName}", 'dahdidahdit-unsigned-aligned.apk'
		standardInput System.in
	}

	println("Aligned and signed APK at ${outputDir}/${apkName}");
}


task printVersionName {
    doLast {
        println android.defaultConfig.versionName
    }
}

